-- Skip update in case installation has been done by 00.99.00.SqlDataProvider (installs everything until and including 08.11.00)
IF NOT EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[ToSIC_SexyContent_Templates]') AND TYPE IN (N'U')) AND
EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[ToSIC_EAV_Apps]') AND TYPE IN (N'U'))
BEGIN
	RETURN
END

-- Because this SQL should not be validated if the condition above is met (return is called), it's wrapped in an EXEC
EXEC('
-- make sure sql rolls back automatically in case of error.
SET XACT_ABORT ON

BEGIN TRANSACTION SexyContentUpdate;

EXEC ToSIC_EAV_ChangeLogAdd ''System''

DECLARE @ChangeLogID int
-- Mod 2016-04-18 2dm change sql-azr detection to only apply exception on V12 as V14 supports the ContextInfo
IF SERVERPROPERTY(''edition'') <> ''SQL Azure'' OR CAST(SERVERPROPERTY(''ProductVersion'') AS CHAR(2)) >= ''12''
	SET @ChangeLogID = CONVERT(int,CONVERT(varbinary(4),CONTEXT_INFO()))
ELSE
	SET @ChangeLogID = 1
SELECT @ChangeLogID

DECLARE @AppAttributeSetID int
SELECT @AppAttributeSetID = AttributeSetID FROM ToSIC_EAV_AttributeSets
WHERE StaticName = ''2SexyContent-App'' AND Scope = N''2SexyContent-App'' AND UsesConfigurationOfAttributeSet IS NULL

-- Insert Attribute OriginalId
INSERT INTO ToSIC_EAV_Attributes (StaticName, [Type], ChangeLogCreated)
VALUES (''OriginalId'', ''String'', @ChangeLogID)
DECLARE @AttributeID int
SET @AttributeID = SCOPE_IDENTITY()

INSERT INTO ToSIC_EAV_AttributesInSets (AttributeSetID, AttributeID, IsTitle, SortOrder, AttributeGroupID)
VALUES (@AppAttributeSetID, @AttributeID, 0, 7, 1)

-- Commit the transaction
COMMIT TRANSACTION SexyContentUpdate;
--ROLLBACK TRANSACTION SexyContentUpdate;')